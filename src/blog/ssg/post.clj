(ns blog.ssg.post
  (:require
   [clojure.string :as str])
  (:import
   [java.time LocalDate LocalDateTime]
   [java.time.format DateTimeFormatter]
   [java.util Locale]))

(def ^:private fmt-full
  (DateTimeFormatter/ofPattern "yyyy-MM-dd EEE HH:mm" Locale/ENGLISH))

(def ^:private fmt-short
  (DateTimeFormatter/ofPattern "yyyy-MM-dd EEE" Locale/ENGLISH))

(def ^:private fmt-display
  (DateTimeFormatter/ofPattern "MMMM d, yyyy" Locale/ENGLISH))

(defn parse-org-date
  "Parse an org-mode date string like <2023-01-22 Sun 19:13> or <2023-01-22 Sun>."
  [s]
  (let [s (-> s
              (str/replace #"[<>]" "")
              str/trim)]
    (try
      (LocalDateTime/parse s fmt-full)
      (catch Exception _
        (.atStartOfDay (LocalDate/parse s fmt-short))))))

(defn format-date
  "Format a LocalDateTime for human display."
  [date]
  (.format date fmt-display))

(defn ->slug [s] (str/replace s "." "-"))

(defn post-title [post] (:title post))
(defn post-date [post] (:date post))
(defn post-slug [post] (:slug post))
(defn post-content [post] (:content post))
(defn post-ref [post key] (get (:metadata post) key))
(defn post-tags [post] (get (:metadata post) :tags []))

(defn make-post
  "Construct a post map from a slug, parsed metadata map, and raw HTML content string."
  [slug metadata content]
  {:slug     slug
   :title    (:title metadata "Untitled")
   :date     (:date metadata)
   :content  content
   :metadata (dissoc metadata :title :date)})

(defn posts-reverse-chronological
  "Sort posts newest-first."
  [posts]
  (sort-by :date #(compare %2 %1) posts))
